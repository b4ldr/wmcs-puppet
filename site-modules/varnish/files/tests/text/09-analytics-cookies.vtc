varnishtest "cache_text analytics cookies"

server s1 {
    rxreq
    txresp

    rxreq
    txresp
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

# We return Set-Cookie multiple times: for WMF-Last-Access,
# WMF-Last-Access-Global and for GeoIP. logexpect seems to be the only sane way
# to check their values.
logexpect l1 -v v1 -g request {
    expect  0 1001  Begin  "^req .* rxreq"
    expect  * = RespHeader "Set-Cookie: GeoIP="
    expect  * = End
} -start

logexpect l2 -v v1 -g request {
    expect  0 1001  Begin  "^req .* rxreq"
    expect  * = RespHeader "^Set-Cookie: WMF-Last-Access=[0-9]{2}-[A-Za-z]{3}-[0-9]{4};Path=/;HttpOnly;secure;Expires="
    expect  * = End
} -start

logexpect l3 -v v1 -g request {
    expect  0 1001  Begin  "^req .* rxreq"
    expect  * = RespHeader "^Set-Cookie: WMF-Last-Access-Global=[0-9]{2}-[A-Za-z]{3}-[0-9]{4};Path=/;Domain=\\.[a-z-.]+;HttpOnly;secure;Expires="
    expect  * = End
} -start

client c1 {
    txreq -url "/" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "X-Client-IP: 91.198.174.192"
    rxresp
    expect resp.http.Strict-Transport-Security == "max-age=106384710; includeSubDomains; preload"
    expect resp.http.X-Analytics == "https=1;nocookies=1"
    expect resp.http.X-Client-IP == "91.198.174.192"

    txreq -url "/" -hdr "Host: it.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Cookie: WMF-Last-Access-Global=2010-01-01"
    rxresp
    expect resp.http.X-Analytics == "https=1"
} -run

logexpect l1 -wait
logexpect l2 -wait
logexpect l3 -wait
