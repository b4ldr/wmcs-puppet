varnishtest "cache_text insecure post forbidden"

server s1 {
    rxreq
    txresp
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    # Insecure GETs are redirected to secure URL
    txreq -url "/" -hdr "Host: en.wikipedia.org"
    rxresp
    expect resp.status == 301
    expect resp.http.location == "https://en.wikipedia.org/"

    # Insecure POSTs are forbidden
    txreq -req POST -url "/" -hdr "Host: en.wikipedia.org"
    rxresp
    expect resp.status == 403
    expect resp.body ~ "Error: 403, Insecure Request Forbidden - use HTTPS - https://lists.wikimedia.org/pipermail/mediawiki-api-announce/2016-May/000110.html"
} -run
