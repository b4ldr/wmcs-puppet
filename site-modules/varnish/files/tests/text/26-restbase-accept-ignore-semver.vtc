varnishtest "RESTBase: ignore semver patch in Accept header"

server s1 {
    rxreq
    expect req.http.Accept ~ "/1.2.0"
    txresp

    rxreq
    expect req.http.Accept ~ "/0.12.0"
    txresp
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    txreq -url "/api/rest_v1/banana" -hdr "X-Forwarded-Proto: https" -hdr "Host: en.wikipedia.org" -hdr "Accept: profile=\"https://www.mediawiki.org/wiki/Specs/Summary/1.2.999\\\""
    rxresp
    expect resp.status == 200

    txreq -url "/api/rest_v1/potato" -hdr "X-Forwarded-Proto: https" -hdr "Host: en.wikipedia.org" -hdr "Accept: application/json; charset=utf-8; profile=\"https://www.mediawiki.org/wiki/Specs/mobile-sections/0.12.999\\\""
    rxresp
    expect resp.status == 200
} -run
