varnishtest "cache_text Vary: Cookie"

server s1 {
    # Response without "Vary: Cookie"
    rxreq
    expect req.url == "/no-vary-cookie"
    txresp

    # Response *with* "Vary: Cookie"
    rxreq
    expect req.url == "/vary-cookie"
    txresp -hdr "Vary: Cookie"

    # Response *with* "Vary: Cookie"
    rxreq
    expect req.url == "/vary-cookie"
    txresp -hdr "Vary: Cookie"
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    # Session cookie, response without "Vary: Cookie"
    txreq -url "/no-vary-cookie" -hdr "Host: en.wikipedia.org" -hdr "Cookie: enwikiSession=42;" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    expect resp.http.Vary == <undef>

    # Session cookie, response with "Vary: Cookie"
    txreq -url "/vary-cookie" -hdr "Host: en.wikipedia.org" -hdr "Cookie: enwikiSession=42;" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    expect resp.http.Vary == "Cookie"

    # Non-Session cookie, response with "Vary: Cookie"
    txreq -url "/vary-cookie" -hdr "Host: en.wikipedia.org" -hdr "Cookie: WMF-Last-Access=25-Feb-2017;" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    expect resp.http.Vary == "Cookie"
} -run
